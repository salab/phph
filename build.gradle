import com.github.jengelman.gradle.plugins.shadow.transformers.*

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.47.0'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

dependencies {
    implementation 'com.github.YoshikiHigo:MPAnalyzer:v0.1.1'
    implementation 'com.github.Durun:nitron:v0.6'

    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.6.0.202305301015-r'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'ch.qos.logback:logback-classic:1.2.12'
    implementation 'info.picocli:picocli:4.7.4'
    implementation 'commons-io:commons-io:2.13.0'
    implementation 'org.apache.commons:commons-exec:1.3'
    implementation 'com.google.guava:guava:32.1.1-jre'

    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
    implementation 'org.jdbi:jdbi3-core:3.39.1'
    implementation 'org.jdbi:jdbi3-sqlobject:3.39.1'

    implementation 'org.springframework.boot:spring-boot-dependencies:2.7.14'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.7.14'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.7.14'
    //implementation 'org.springframework.boot:spring-boot-devtools:2.6.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

configurations.implementation {
    exclude group: 'org.slf4j', module: 'slf4j-nop'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "jp.ac.titech.c.phph.Application"
        attributes "Multi-Release": "true"
    }
}

shadowJar {
    // https://github.com/spring-projects/spring-boot/issues/1828
    // Required for Spring
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories' ]
        mergeStrategy = "append"
    }
}
